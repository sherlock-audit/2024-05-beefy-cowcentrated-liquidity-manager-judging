Soft Sangria Lion

high

# VeloSwapUtils::swap() do not provide 0 value as minAmountOut whilst not checking received amount afterwards

## Summary

When `harvest` is called, fees are charged and `unirouter.execute` is called within `VeloSwapUtils` to swap `token0` to `native` for fee payment. However `minAmountOut` within the swap is set to 0, meaning the swap is vulnerable to slippage and price manipulation.

## Vulnerability Detail
[VeloSwapUtils::swap()](https://github.com/sherlock-audit/2024-05-beefy-cowcentrated-liquidity-manager/blob/main/cowcentrated-contracts/contracts/utils/VeloSwapUtils.sol#L22-L41)
```
    function swap(
        address _router,
        bytes memory _path,
        uint256 _amountIn,
        bool _isV3
    ) internal {
        if (_isV3) {
>>          bytes memory input = abi.encode(address(this), _amountIn, 0, _path, true); // minAmount0 set to 0
            bytes[] memory inputs = new bytes[](1);
            inputs[0] = input;
>>          IVeloRouter(_router).execute(abi.encodePacked(V3_SWAP_EXACT_IN), inputs, block.timestamp);
        } else {
            address[] memory route = pathToRoute(_path);
            bytes memory input = abi.encode(address(this), _amountIn, 0, route, true);
            bytes[] memory inputs = new bytes[](1);
            inputs[0] = input;

            IVeloRouter(_router).execute(abi.encodePacked(V2_SWAP_EXACT_IN), inputs, block.timestamp);
        }
    }
```
When calling `execute` on the router, the `minAmountOut` amount is set as 0.
[StrategyPassiveManagerVelodrome::_chargeFees()](https://github.com/sherlock-audit/2024-05-beefy-cowcentrated-liquidity-manager/blob/main/cowcentrated-contracts/contracts/strategies/velodrome/StrategyPassiveManagerVelodrome.sol#L478-L513)
```solidity
    function _chargeFees(address _callFeeRecipient, uint256 _amount) private returns (uint256 _amountLeft){
        /// Fetch our fee percentage amounts from the fee config.
        IFeeConfig.FeeCategory memory fee = getFees();

        /// We calculate how much to swap and then swap both tokens to native and charge fees.
        uint256 nativeEarned;
        if (_amount > 0) {
            // Calculate amount of token 0 to swap for fees.
            uint256 amountToSwap = _amount * fee.total / DIVISOR;
            _amountLeft = _amount - amountToSwap;
            
            // If token0 is not native, swap to native the fee amount.
            uint256 out;
            uint256 nativeBefore = IERC20Metadata(native).balanceOf(address(this));
            if (output != native) {
>>              VeloSwapUtils.swap(unirouter, outputToNativePath, amountToSwap, true);
                out = IERC20Metadata(native).balanceOf(address(this)) - nativeBefore;
            }
            
            // Add the native earned to the total of native we earned for beefy fees, handle if token0 is native.
            if (output == native)  nativeEarned += amountToSwap;
            else nativeEarned += out;
        }
```
`swap` is called when charging fees and within the function there is no check on how much was received from the swap. Meaning that the amount swapped may be affected by slippage and front-running, as the swap occurs on the `outputToNativePath` and is not monitored by `StrategyPassiveManagerVelodrome` for calm periods.

## Impact

When `harvest` is called and fees are charged, `unirouter` is utilised to swap `token0` to `native`. However no checks are made on the amount received from the swap. This can lead to front-running and losing out on fees by the protocol.

High risk as this will occur on each `harvest` call whenever fees are being charged. Meaning losses will keep adding up for the protocol.

## Code Snippet

[VeloSwapUtils::swap()](https://github.com/sherlock-audit/2024-05-beefy-cowcentrated-liquidity-manager/blob/main/cowcentrated-contracts/contracts/utils/VeloSwapUtils.sol#L22-L41)
[StrategyPassiveManagerVelodrome::_chargeFees()](https://github.com/sherlock-audit/2024-05-beefy-cowcentrated-liquidity-manager/blob/main/cowcentrated-contracts/contracts/strategies/velodrome/StrategyPassiveManagerVelodrome.sol#L478-L513)

## Tool used

Manual Review

## Recommendation

Check TWAP of the route pools to ensure that they are not being manipulated before getting fees. However this is not a trivial task.
