Cold Wool Shell

high

# StrategyPassiveManagerVelodrome does not take into account unharvested fees

## Summary
`StrategyPassiveManagerVelodrome.sol` does not take into account unharvested fees, which will lead to inaccurate calculations and losing fees, as the function `balances()` will return an incorrect value of `total0`.

## Vulnerability Detail
Before every `deposit/withdraw` vault calls `beforeAction()` function. This is necessary for the calculation of amounts in `deposit/withdraw` functions in vault to be accurate:
```solidity
/// @notice Called during deposit and withdraw to remove liquidity and harvest fees for accounting purposes.
    function beforeAction() external {
        _onlyVault();
        _claimEarnings();  
        _removeLiquidity();
    }
```
The protocol expects this function to harvest a fees (for accounting purposes). But instead this function actually increases the amount of unharvested fees (because of calling `_claimEarnings()`):
```solidity
/// @notice Internal function to claim rewards from the gauge and collect them.
    function _claimEarnings() private {
        // Claim rewards
        uint256 feeBefore = IERC20Metadata(output).balanceOf(address(this));

        if (positionMain.nftId != 0) ICLGauge(gauge).getReward(positionMain.nftId);
        if (positionAlt.nftId != 0) ICLGauge(gauge).getReward(positionAlt.nftId);

        uint256 claimed = IERC20Metadata(output).balanceOf(address(this)) - feeBefore;
        fees = fees + claimed;

        emit ClaimedFees(claimed);
    }
```
Next, the function `balances()` is called. It is expected to return `balance of this contract + balance of positions - feesUnharvested`:
```solidity
function balances() public view returns (uint256 token0Bal, uint256 token1Bal) {
        (uint256 thisBal0, uint256 thisBal1) = balancesOfThis();
        (uint256 poolBal0, uint256 poolBal1,,,,) = balancesOfPool();

        uint256 total0 = thisBal0 + poolBal0;
        uint256 total1 = thisBal1 + poolBal1;

        // For token0 and token1 we return balance of this contract + balance of positions - feesUnharvested.  
        return (total0, total1);
    }
```
But it does not take into account unharvested fees and only returns `balance of this contract + balance of positions`.

Also note:
>This code comments are not outdated based on contextual evidence - the protocol clearly uses fees, and the comments and the order of calling some functions indicate that the protocol expects fees to be harvested.

## Impact
As stated in the comments, `fees` is `token0 `:
```solidity
//* @return _amountLeft The amount of token0 left after fees.

// Calculate amount of token 0 to swap for fees.
            uint256 amountToSwap = _amount * fee.total / DIVISOR;
            _amountLeft = _amount - amountToSwap;
            
            // If token0 is not native, swap to native the fee amount.
```
In this case function `balances()` will always return too big value of `total0`. It will lead to incorrect calculation in `deposit/withdraw` in `BeefyVaultConcLiq.sol`. Also `panic()` finction relies on the value of this function and will not revert even if amount of `token0` in the strategy is less that `bal0`, so this check will not revert, causing to bypassing slippage check:
```solidity
if (bal0 < _minAmount0 || bal1 < _minAmount1) revert TooMuchSlippage();
```

Also note that `fees` is rewards from the gauge, not part of deposited funds, so they should not be withrawable.
Here is attack scenario (some details omitted for simplicity):
 - User deposits 1000 tokens, now he has 1000 shares
 - Protocol receives rewards from the gauge
 - `token0.balanceOf(address(this))` = 1050, where user's deposit = 1000, and unharvested fees = 50 tokens
 - User redeems 1000 shares and receives 1050 tokens:
 _amount0 = (_bal0 * _shares) / _totalSupply = (1050 * 1000) / 1000 = 1050
```solidity
        strategy.beforeAction();

        uint256 _totalSupply = totalSupply();
        _burn(msg.sender, _shares);

        (uint256 _bal0, uint256 _bal1) = balances();

        uint256 _amount0 = (_bal0 * _shares) / _totalSupply;
        uint256 _amount1 = (_bal1 * _shares) / _totalSupply;

        strategy.withdraw(_amount0, _amount1);

        IERC20Upgradeable(token0).safeTransfer(msg.sender, _amount0);
```
- User steals all protocol's unharvested fees
## Code Snippet
[Link 1](https://github.com/sherlock-audit/2024-05-beefy-cowcentrated-liquidity-manager/blob/main/cowcentrated-contracts/contracts/strategies/velodrome/StrategyPassiveManagerVelodrome.sol#L196-L201)
[Link 2](https://github.com/sherlock-audit/2024-05-beefy-cowcentrated-liquidity-manager/blob/main/cowcentrated-contracts/contracts/strategies/velodrome/StrategyPassiveManagerVelodrome.sol#L515-L529)
## Tool used

Manual Review

## Recommendation
Change the code as follows:
```diff
/// @notice Called during deposit and withdraw to remove liquidity and harvest fees for accounting purposes.
    function beforeAction() external {
        _onlyVault();
+       harvest();
-       _claimEarnings();  
        _removeLiquidity();
    }

/** 
     * @notice Returns total token balances in the strategy.
     * @return token0Bal The amount of token0 in the strategy.
     * @return token1Bal The amount of token1 in the strategy.
    */
function balances() public view returns (uint256 token0Bal, uint256 token1Bal) {
        (uint256 thisBal0, uint256 thisBal1) = balancesOfThis();
        (uint256 poolBal0, uint256 poolBal1,,,,) = balancesOfPool();

+       uint256 total0 = thisBal0 + poolBal0 - fees;
-       uint256 total0 = thisBal0 + poolBal0;
        uint256 total1 = thisBal1 + poolBal1;

        // For token0 and token1 we return balance of this contract + balance of positions - feesUnharvested.  
        return (total0, total1);
    }
```